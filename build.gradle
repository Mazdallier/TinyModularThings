buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

repositories {
    maven {
        name "chickenbones"
        url "http://chickenbones.net/maven/"
    }
}

apply plugin: 'forge'

def api_version = "2.0.1"
def tmt_version = "2.0.5"

version = tmt_version

group= "speiger.src.tinymodularthings" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "TinyModularThings"

minecraft {
    version = "1.6.4-9.11.1.964"
    assetDir = "eclipse/assets"
}

dependencies {
    compile 'codechicken:CodeChickenCore:1.6.4-0.9.0.9:dev'
    compile 'codechicken:NotEnoughItems:1.6.4-1.6.1.9:dev'
    // In ./libs folder:
    // buildcraft-A-1.6.4-4.2.2.jar
    // forestry-A-2.3.0.1_dev.jar
    // industrialcraft-2-dev-deobf_2.0.397-experimental.jar
    // MFR-API-2.7.8-483.zip
    // Railcraft_1.6.4-8.4.0.0.jar
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'api_version':api_version,
               'tmt_version':tmt_version,
               'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task updateVersion(type: Copy) {
    from(sourceSets.main.java.srcDirs)
    into 'build/sources/java'
    expand 'api_version':api_version, 'tmt_version':tmt_version
}

compileJava.dependsOn updateVersion