buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    maven {
        name "chickenbones"
        url "http://chickenbones.net/maven/"
    }
}

apply plugin: 'forge'

def api_version = "2.0.1"
def tmt_version = "2.0.5"
version = tmt_version
group= "speiger" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "TinyModularThings"

minecraft {
    version = "1.7.10-10.13.0.1200"
    runDir = "run"
}

dependencies {
    compile 'codechicken:CodeChickenLib:1.7.10-1.1.1.102:dev'
    compile 'codechicken:NotEnoughItems:1.7.10-1.0.2.15:dev'
    compile 'com.mod-buildcraft:buildcraft:6.0.17:dev'
    // In ./libs folder:
    // forestry_1.7.10-2.4.2.67-dev.jar ---> http://ic2api.player.to:8080/job/Forestry/67/
    // industrialcraft-2-2.2.596-experimental-dev.jar ---> http://ic2api.player.to:8080/job/IC2_experimental/596/
    // MineFactoryReloaded-API-[1.7.10]2.8.0RC2-572.zip ---> https://jenkins.mc.dryware.org/job/skyboy-MineFactoryReloaded/572/
    // Railcraft_1.7.10-9.2.2.0-dev.jar --> http://www.railcraft.info/releases/
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'api_version':api_version,
               'tmt_version':tmt_version,
               'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task updateVersion(type: Copy) {
    from(sourceSets.main.java.srcDirs)
    into 'build/sources/java'
    expand 'api_version':api_version, 'tmt_version':tmt_version
}
compileJava.dependsOn updateVersion

jar {
    exclude "powercrystals/**"
}

task extractAPI(type: Copy) {
    outputs.upToDateWhen { false }
    from (zipTree(file('./libs/MineFactoryReloaded-API-[1.7.10]2.8.0RC2-572.zip')))
    include "**/*.java"
    into file('./src/main/java')
}

eclipseProject.dependsOn extractAPI
